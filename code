//Genius by AcaciaCalegari

//-----------------------#definir as variaveis ------------------------//
#define LED_A 5 //amarelym
#define LED_B 6 //vermelhao
#define LED_C 7 //verdym
 
#define BUTTON_A 8
#define BUTTON_B 9
#define BUTTON_C 10
 
#define BUZZER 11
 
#define TOM_A 1200
#define TOM_B 1600
#define TOM_C 2000
 
#define TEMPO_PISCA 600 //milisegundo
#define TEMPO_ENTRE 150
 
#define QT_SEQ 6 //sequencia
 
int sorteados[QT_SEQ]; // QT_SEQ eh o tamanho da sequencia dos leds
 
//--------------Declaração da funçao de cada elemento---------------//
void setup() {
  Serial.begin(9600);
 
  pinMode(LED_A, OUTPUT); //os leds vao ser a saida digital
  pinMode(LED_B, OUTPUT);
  pinMode(LED_C, OUTPUT);
 
  pinMode(BUTTON_A, INPUT_PULLUP); //os botoes serao as entradas, tb no digital
  pinMode(BUTTON_B, INPUT_PULLUP);
  pinMode(BUTTON_C, INPUT_PULLUP);
 
  pinMode(BUZZER, OUTPUT); //saida de som , com varios tipos de tom
 
  randomSeed(analogRead(0)); //Sorteia uma nova sequencia toda vez que liga o Arduino
}
 
//----------Loop do sorteio, leitura de botão e comparacao----------//
void loop() {
  //Acende tudo
  digitalWrite(LED_A, HIGH); //high significa ligado, 5v
  digitalWrite(LED_B, HIGH);
  digitalWrite(LED_C, HIGH);
 
  le_botao(); //Jogador aperta qlqer botaum e já inicia o sorteio
  //Apaga tudo
  digitalWrite(LED_A, LOW); //low signfca dsligado, 0v
  digitalWrite(LED_B, LOW);
  digitalWrite(LED_C, LOW);
  delay(500);
 
//-----------------------Sorteio da sequencia-----------------------//
//QT_SEQ eh o tamanho da sequência - quantas vezes os LEDs piscam
  for(int jogada = 0; jogada < QT_SEQ; jogada ++) {
    sorteados[jogada] = random(3);
    pisca_led(sorteados[jogada]);
    delay(TEMPO_ENTRE);
  } //Fim do sorteio
 
//---------------------Comparação da sequencia----------------------//
  int vez; //Definido fora do for para poder verificar numero de acertos no final
  for(vez = 0; vez < QT_SEQ; vez ++) {
    if(le_botao() == sorteados[vez]) {
      pisca_led(sorteados[vez]);
    } else {
      pisca_led(sorteados[vez]);
      break; //Sai do for
    }
  }
 
//----------------------------Resultado-----------------------------//
  if(vez == QT_SEQ) { //Acertou seq. inteira
    delay(500);
    for(int i = 0; i < 3; i++) {
      tone(BUZZER, 2000);
      delay(200);
      tone(BUZZER, 4000);
      delay(200);
    }
    noTone(BUZZER);
  } else { //Errou algo
    delay(500);
    for(int i = 0; i < 3; i++) {
      tone(BUZZER, 200);
      delay(200);
      tone(BUZZER, 300);
      delay(200);
    }
    noTone(BUZZER);
  }
} //Fim do loop
 
//------------------------Função piscar LED-------------------------//
void pisca_led(int posicao) {
  Serial.println(posicao);
 
  if(posicao == 0) {
    digitalWrite(LED_A, HIGH);
    tone(BUZZER, TOM_A);
    delay(TEMPO_PISCA);
    digitalWrite(LED_A, LOW);
  } else if(posicao == 1) {
    digitalWrite(LED_B, HIGH);
    tone(BUZZER, TOM_B);
    delay(TEMPO_PISCA);
    digitalWrite(LED_B, LOW);
  } else if(posicao == 2) {
    digitalWrite(LED_C, HIGH);
    tone(BUZZER, TOM_C);
    delay(TEMPO_PISCA);
    digitalWrite(LED_C, LOW);
  }
  delay(100);
  noTone(BUZZER);
} //Fim do pisca_led
 
//------------------------Funcao ler os botões----------------------//
int le_botao () {
  int result = -1; //-1 indica que nenhum botaum foi apertado
 
  while(result == -1) {
    if(digitalRead(BUTTON_A) == LOW) {
      result = 0;    //Posicao 0
    } else if(digitalRead(BUTTON_B) == LOW) {
      result = 1;
    } else if(digitalRead(BUTTON_C) == LOW) {
      result = 2;
    }
  }
  return result; //Retorna o inteiro int le_botao
}
